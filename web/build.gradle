def eclipsePluginProjects = subprojects.findAll{it.file('build.properties').exists()}
def exampleProjects = subprojects.findAll{it.name.contains("example")}

configure(eclipsePluginProjects) {
	apply from: "${rootDir}/gradle/eclipse-plugin-project.gradle"
}

configure(subprojects - eclipsePluginProjects) {
	apply plugin: 'java'
	apply from: "$rootDir/gradle/eclipse-settings.gradle"

	sourceSets {
		main.java.srcDir 'src/main/xtext-gen'
		main.java.srcDir 'src/main/xtend-gen'
		test.java.srcDir 'src/test/xtend-gen'
	}
	
	if (JavaVersion.current().isJava8Compatible()) {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:syntax,reference', '-quiet')
		}
	}
}

configure(subprojects - exampleProjects) {
	apply from: "$rootDir/gradle/maven-deployment.gradle"
}